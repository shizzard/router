[doc]
Check if router registers virtual service via control stream.
[enddoc]

[include ../../__common/capture.luxinc]
[include ../../__common/common.luxinc]
[include ../../__common/grpc.luxinc]
[include ../../__common/jq.luxinc]
[include ../../__router/registry-service.luxinc]

[global request-file-init=json/03-init-request.json]
[global request-file-register=json/03-register-request.json]
[global request-file-list=json/03-list-request.json]

[shell router]
  [invoke app-start]

[shell control-stream]
  [progress (init)]
  [invoke grpcc-start ${grpc-service-name} ${grpc-method-control-stream} json ""]
  [invoke grpcc-conn-up]
  [invoke grpcc-prompt]
  [invoke grpcc-send-data ${request-file-init}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-headers]
  [global headers=${out}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-data]
  [global data=${out}]
  [invoke grpcc-prompt]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${headers} .headers.\"content-type\" "application/grpc+proto"]
  [invoke jq-check-file-match ${headers} .headers.\"grpc-user-agent\" "router/"]
  [invoke jq-check-file-match ${headers} .status 200]
  [invoke jq-check-file-match ${data} .event.init_rs.id.id "init-1"]
  [invoke jq-check-file-match ${data} .event.init_rs.result.status SUCCESS]
  [invoke jq-check-file ${data} .event.init_rs.session_id]
  ?^(${capture-uuid})$
  [global session-id=$1]
  [invoke check-ok]
  [progress (session id ${session-id})]

[shell control-stream]
  [progress (register)]
  [invoke grpcc-send-data ${request-file-register}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-data]
  [global data=${out}]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${data} .event.register_virtual_service_rs.id.id "register-1"]
  [invoke jq-check-file-match ${data} .event.register_virtual_service_rs.result.status SUCCESS]

[shell list]
  [progress (list)]
  [invoke grpcc-start-unary ${grpc-service-name} ${grpc-method-list-virtual-services} "json" "" ${request-file-list}]
  [progress (check response)]
  [invoke jq-check-file-match ${headers} .headers.\"content-type\" "application/grpc+proto"]
  [invoke jq-check-file-match ${headers} .headers.\"grpc-user-agent\" "router/"]
  [invoke jq-check-file-match ${headers} .status 200]
  [invoke jq-check-file-match ${data} .services[0].service.stateless.package lg.test.package]
  [invoke jq-check-file-match ${data} .services[0].service.stateless.name StatelessService]
  [invoke jq-check-file-match ${data} .services[0].service.stateless.methods[0].name MethodOne]
  [invoke jq-check-file-match ${data} .services[0].endpoint.host "test.lg"]
  [invoke jq-check-file-match ${data} .services[0].endpoint.port 8137]
  [invoke jq-check-file-match ${trailers} .trailers.\"grpc-status\" 0]
  [invoke jq-check-file-empty ${trailers} .trailers.\"grpc-message\"]

[cleanup]
