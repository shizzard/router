[doc]
Check if router handles agent registration (dynamic) properly.
[enddoc]

[include ${ROUTER_DIR_TESTS_LUX}/__common/capture.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/common.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/grpc.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/jq.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__router/registry-service.luxinc]

[global request-file-init=json/init-request.json]
[global request-file-register-agent=json/register-agent-request.json]

[shell router]
  [invoke app-start]

[shell control-stream]
  [progress (init)]
  [invoke grpcc-start ${grpc-service-name} ${grpc-method-control-stream} json ""]
  [invoke grpcc-conn-up]
  [invoke grpcc-prompt]
  [invoke grpcc-send-data ${request-file-init}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-headers]
  [global headers=${out}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-data]
  [global data=${out}]
  [invoke grpcc-prompt]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${headers} .headers.\"content-type\" "application/grpc+proto"]
  [invoke jq-check-file-match ${headers} .headers.\"grpc-user-agent\" "router/"]
  [invoke jq-check-file-match ${headers} .status 200]
  [invoke jq-check-file-match ${data} .id.tag "init-1"]
  [invoke jq-check-file-match ${data} .event.init_rs.result.status SUCCESS]
  [invoke jq-check-file ${data} .event.init_rs.session_id]
  ?^(${capture-uuid})$
  [global session-id=$1]
  [invoke check-ok]
  [progress (session id ${session-id})]

[shell control-stream]
  [progress (register agent)]
  [invoke grpcc-send-data ${request-file-register-agent}]
  [invoke grpcc-prompt]
  [invoke grpcc-recv-data]
  [global data=${out}]
  [invoke grpcc-prompt]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${data} .id.tag "register-agent-1"]
  [invoke jq-check-file-match ${data} .event.register_agent_rs.result.status SUCCESS]
  [invoke jq-check-file-match ${data} .event.register_agent_rs.agent_id "agent-1"]
  [invoke jq-check-file ${data} .event.register_agent_rs.agent_instance]
  ?^(${capture-uuid})$
  [global agent-instance=$1]
  [invoke check-ok]
  [progress (agent instance ${agent-instance})]

[cleanup]
