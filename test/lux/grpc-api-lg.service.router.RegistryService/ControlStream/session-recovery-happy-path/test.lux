[doc]
Check if router handles session reinitialization within another stream.
[enddoc]

[include ${ROUTER_DIR_TESTS_LUX}/__common/capture.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/common.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/grpc.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__common/jq.luxinc]
[include ${ROUTER_DIR_TESTS_LUX}/__router/registry-service.luxinc]

[global request-file-init=json/init-request.json]
[global template-file-resume=json/resume-request.template.json]
[global request-file-resume=json/resume-request.json]

[shell router]
  [invoke app-start]

[shell control-stream-1]
  [progress (init)]
  [invoke grpcc-start ${grpc-service-name} ${grpc-method-control-stream} json ""]
  [invoke grpcc-conn-up]
  [invoke grpcc-send-data ${request-file-init}]
  [invoke grpcc-recv-headers]
  [global headers=${out}]
  [invoke grpcc-recv-data]
  [global data=${out}]
  [invoke grpcc-terminate]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${headers} .headers.\"content-type\" "application/grpc+proto"]
  [invoke jq-check-file-match ${headers} .headers.\"grpc-user-agent\" "router/"]
  [invoke jq-check-file-match ${headers} .status 200]
  [invoke jq-check-file-match ${data} .id.tag "init-1"]
  [invoke jq-check-file-match ${data} .event.init_rs.result.status SUCCESS]
  [invoke jq-check-file ${data} .event.init_rs.session_id]
  ?^(${capture-uuid})$
  [global session-id=$1]
  [invoke check-ok]
  [progress (session id ${session-id})]

[shell control-stream-2]
  [progress (prepare second init request)]
  !sed 's~%PLACEHOLDER%~${session-id}~' ${template-file-resume} > ${request-file-resume}
  ?${sh-prompt}
  [progress (init)]
  [invoke grpcc-start ${grpc-service-name} ${grpc-method-control-stream} json ""]
  [invoke grpcc-conn-up]
  [invoke grpcc-send-data ${request-file-resume}]
  [invoke grpcc-recv-headers]
  [global headers=${out}]
  [invoke grpcc-recv-data]
  [global data=${out}]

[shell jq-check]
  [progress (check response)]
  [invoke jq-check-file-match ${headers} .headers.\"content-type\" "application/grpc+proto"]
  [invoke jq-check-file-match ${headers} .headers.\"grpc-user-agent\" "router/"]
  [invoke jq-check-file-match ${headers} .status 200]
  [invoke jq-check-file-match ${data} .id.tag "resume-1"]
  [invoke jq-check-file-match ${data} .event.init_rs.result.status SUCCESS]
  [invoke jq-check-file-match ${data} .event.init_rs.session_id ${session-id}]
  [progress (session id ${session-id})]

[cleanup]
